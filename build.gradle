plugins {
    alias libs.plugins.loom apply false
}

allprojects {
    group = "band.kessokuteatime.kessokulib"
    apply plugin: 'idea'
    apply plugin: 'java-library'
    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }
    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    dependencies {
        implementation 'com.google.auto.service:auto-service-annotations:1.1.1'
        annotationProcessor 'com.google.auto.service:auto-service:1.1.1'
    }
}
def moduleDependencies(project, List<String> depNames) {
    def deps = depNames.iterator().collect { project.dependencies.project(path: ":$it", configuration: 'namedElements') }
    project.dependencies {
        deps.each {
            api it
        }
    }
}