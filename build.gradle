import org.jetbrains.gradle.ext.Gradle

plugins {
    alias libs.plugins.loom apply false
    alias libs.plugins.architectury
    alias libs.plugins.shadow apply false
    alias libs.plugins.spotless
    alias libs.plugins.idea.ext
    id 'maven-publish'
}

architectury {
    minecraft = libs.versions.minecraft.get()
}

idea {
    project {
        settings {
            runConfigurations {
                "Run Client"(Gradle) {
                    taskNames = ["runClient"]
                }
                "Spotless"(Gradle) {
                    taskNames = ["spotlessApply"]
                }
            }
        }
    }
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: "com.diffplug.spotless"
    apply plugin: 'maven-publish'

    assemble.dependsOn spotlessApply

    repositories {
        maven { url "https://maven.neoforged.net/releases/" }
        maven { url "https://jitpack.io" }
    }

    dependencies {
        minecraft(libs.minecraft)
        mappings(loom.layered {
            mappings(variantOf(libs.yarn) { classifier("v2") })
            mappings(libs.yarn.patch)
        })
        implementation("com.google.auto.service:auto-service-annotations:1.1.1")
        annotationProcessor("com.google.auto.service:auto-service:1.1.1")
    }

    spotless {
        java {
            licenseHeaderFile(rootProject.file("HEADER"))
            importOrder('java|javax', '', 'net.minecraft', 'net.fabricmc|net.neoforged')
            removeUnusedImports()
            indentWithSpaces()
            trimTrailingWhitespace()
        }
    }

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        repositories {
            maven {
                name = "KessokuLib"
                url = "https://maven.cloudsmith.io/kessokuteatime/kessokulib/"

                credentials {
                    username = "tex-true"
                    password = System.getenv("CLOUDSMITH_MAVEN_TOKEN")
                }
            }
        }
    }
}

allprojects {
    apply plugin: "com.diffplug.spotless"

    configurations {
        moduleImplementation {
            canBeResolved = true
            canBeConsumed = false
        }
        compileClasspath.extendsFrom moduleImplementation
        runtimeClasspath.extendsFrom moduleImplementation
        moduleInclude {
            canBeResolved = true
            canBeConsumed = false
        }
        include.extendsFrom moduleInclude
    }
}
