plugins {
    alias libs.plugins.loom apply false
    alias libs.plugins.architectury
    alias libs.plugins.shadow apply false
    alias libs.plugins.spotless apply false
}

architectury {
    minecraft = libs.versions.minecraft.get()
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'maven-publish'

    assemble.dependsOn spotlessApply

    repositories {
        maven { url "https://maven.neoforged.net/releases/" }
    }

    dependencies {
        minecraft libs.minecraft
        mappings loom.layered {
            mappings(variantOf(libs.yarn) { classifier("v2") })
            mappings libs.yarn.patch
        }
        implementation 'com.google.auto.service:auto-service-annotations:1.1.1'
        annotationProcessor 'com.google.auto.service:auto-service:1.1.1'
    }

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    spotless {
        java {
            importOrder('java|javax', '', 'net.minecraft', 'net.fabricmc|net.neoforged')
            removeUnusedImports()
            indentWithSpaces()
            trimTrailingWhitespace()
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }
        repositories {
        }
    }
}
