plugins {
    id 'com.github.johnrengelman.shadow'
}

base {
    archivesName = rootProject.name + "-base"
}

group = "band.kessoku.lib.base"
version = libs.versions.mod.get() + "+neoforge." + libs.versions.minecraft.get()

architectury {
    platformSetupLoomIde()
    neoForge()
}

repositories {
    maven { url "https://maven.neoforged.net/releases/" }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    neoForge libs.neo
    common(project(path: ':base-common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':base-common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}